plugins {
    id "java"
    id "application"
    id "com.github.johnrengelman.shadow" version "4.0.2"
}

version "0.1"
group "org.graalvm.nativeimage.demos.rmi"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

mainClassName = "org.graalvm.nativeimage.demos.rmi.Main"
def imageName = "rmi"

task runAgent(type: JavaExec) {
    def defaultNativeImageResDir = "/META-INF/native-image/org.graalvm.nativeimage.demos.rmi/rmi"
    jvmArgs "-agentlib:native-image-agent=config-output-dir=${sourceSets.main.resources.srcDirs.first()}/${defaultNativeImageResDir}"
    classpath = sourceSets.main.runtimeClasspath
    main = mainClassName
}
runAgent.dependsOn(classes)

task nativei(type: Exec) {
    def execLine = ["native-image"]
    if (project.hasProperty("debug")) {
        execLine.add('--debug-attach')
    }
    execLine.addAll(['--verbose', '-H:+PrintClassInitialization', '--enable-all-security-services', "-H:Path=${project.buildDir}"])
    execLine.addAll(['--no-fallback', '-cp', "${project.libsDir}/*"])
    setCommandLine(execLine)
}
nativei.dependsOn(assemble)

task runnative(type: Exec) {
    commandLine "${project.buildDir}/$imageName"
}
runnative.dependsOn(nativei)

